// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "src/memmap";
import "registers";

import "audio";
import "textbox";
import "interrupts";
import "game/ms_palettes";
import "game/metasprites";
import "game/metatiles";
import "game/gamestate";

import "common/reset";

import "src/engine-hooks";


let FRAMES_PER_SECOND = 60;


in lowram {
    // A shadow of the INIDISP register
    var _brightness_buffer : u8;


    // Flag to determine which scroll shadow variable goes to BG1 and BG2.
    //
    // If `swapBg1AndBg2ScrollShadows` is zero, bgA is BG1 and bgB is BG2.
    // If `swapBg1AndBg2ScrollShadows` is non-zero, bgA is BG2 and bgB is BG1.
    //
    // NOTE: This variable is reset in `reset_registers()`.
    //
    // (byte flag)
    var swapBg1AndBg2ScrollShadows : u8 in __swapBg1AndBg2ScrollShadows;

    // A shadow of the BG1 and BG2 scroll registers.
    //
    // See `swapBg1AndBg2ScrollShadows`.
    //
    // (4x u16 words)
    var bgA_scroll_x : u16;
    var bgA_scroll_y : u16;
    var bgB_scroll_x : u16;
    var bgB_scroll_y : u16;
}


in code {



// Pause the game for a second
//
// DB = 0x7e
#[mem8, idx8]
func wait_one_second() {

    a = FRAMES_PER_SECOND;

    do {
        push8(a);

        wait_frame();

        a = pop8();
        a--;
    } while !zero;
}



// Wait until the end of the current frame, process VBlank routines.
//
// REQUIRES: NMI enabled, IRQs disabled
//
// DB = lowram
func wait_frame() {
    // MUST NOT modify zpTmp variables.

    push8(p);
    push8(data_bank);

    push8(program_bank);
    data_bank = pop8();
// DB = 0x80


    mem8();
    idx16();
    #[mem8, idx16] {
        a = _brightness_buffer | INIDISP_FORCE_BLANK;

        wait_until_interrupt();

        // Force-blank screen to prevent corrupt graphics on VBlank overrun
        INIDISP = a;


        metasprites.update_oam__vblank();
        ms_palettes.update_cgram__vblank();

        metatiles.transitions.transfer_buffer__vblank();
        second_layer.transfer_buffers__vblank();

        textbox.transfer_buffer__vblank();


        // Update scroll registers

        a = swapBg1AndBg2ScrollShadows;
        if zero {
            BG1HOFS = a = *(&<:bgA_scroll_x as *u8);
            BG1HOFS = a = *(&>:bgA_scroll_x as *u8);
            BG1VOFS = a = *(&<:bgA_scroll_y as *u8);
            BG1VOFS = a = *(&>:bgA_scroll_y as *u8);

            BG2HOFS = a = *(&<:bgB_scroll_x as *u8);
            BG2HOFS = a = *(&>:bgB_scroll_x as *u8);
            BG2VOFS = a = *(&<:bgB_scroll_y as *u8);
            BG2VOFS = a = *(&>:bgB_scroll_y as *u8);
        }
        else {
            BG1HOFS = a = *(&<:bgB_scroll_x as *u8);
            BG1HOFS = a = *(&>:bgB_scroll_x as *u8);
            BG1VOFS = a = *(&<:bgB_scroll_y as *u8);
            BG1VOFS = a = *(&>:bgB_scroll_y as *u8);

            BG2HOFS = a = *(&<:bgA_scroll_x as *u8);
            BG2HOFS = a = *(&>:bgA_scroll_x as *u8);
            BG2VOFS = a = *(&<:bgA_scroll_y as *u8);
            BG2VOFS = a = *(&>:bgA_scroll_y as *u8);
        }


        engine_hooks.wait_frame__vblank__inline();


        INIDISP = a = _brightness_buffer;
    }

    idx8();
    mem16();
    #[mem16, idx8] {
        metatiles.process_draw_tile_buffer__vblank();
    }

    mem16_idx16();
    #[mem16, idx16] {
        aa = *(&nmiCounter as *u16) & 0xff;

        frameCounter_loword = aa = aa + frameCounter_loword;
        if zero {
            frameCounter_hiword++;
        }

        engine_hooks.wait_frame__after_vblank__mem16_idx16__inline();
    }

    mem8();
    #[mem8, idx16] {
        nmiCounter = 0;

        audio.process();
    }

    mem8_idx8();
    #[mem8, idx8] {
        engine_hooks.wait_frame__after_vblank__mem8_idx8__inline();
    }


    data_bank = pop8();
    p = pop8();

// DB = unknown
}



// Change the brightness of the display.
//
// NOTE: Will also wait until the end of the current frame
//
// DB = lowram
#[mem8]
func wait_frame_and_change_brightness(new_brightness : u8 in a) {
    _brightness_buffer = a;

    ^return wait_frame();
}



// Preform a fadeout animation until the display is disabled (force-blank).
//
// NOTE: If the system is in force-blank, this function will not fadeout.
//
// RETURNS: Force-blank
//
// DB = lowram
#[mem8, idx8]
func wait_multiple_frames_and_fadeout() {
    a = _brightness_buffer;
    if negative {
        ^return wait_frame();
    }
    if a >= 15 + 1 {
        a = 15;
    }
    do {
        a--;
        wait_frame_and_change_brightness(a);
        a = _brightness_buffer;
    } while !negative;

// In force blank.
}



// Preform a fadeout animation until the display is disabled (force-blank) at one-third speed.
//
// NOTE: If the system is in force-blank, this function will not fadeout.
//
// RETURNS: Force-blank
//
// DB = lowram
#[mem8, idx8]
func wait_multiple_frames_and_fadeout__slow() {
    while true {
        a = _brightness_buffer;
        if negative {
            // In force blank.
            return;
        }
        if a >= 15 + 1 {
            a = 15;
        }
        a--;
        wait_frame_and_change_brightness(a);

        wait_frame();
        wait_frame();
    }
}


// Preform a fade-in animation.
//
// NOTE: If the screen's brightness if full, this function will not do anything.
//
// DB = lowram
#[mem8, idx8]
func wait_multiple_frames_and_fadein() {
    a = _brightness_buffer;
    if a >= 15 {
        a = 0;
    }
    do {
        wait_frame_and_change_brightness(a);

        a = _brightness_buffer;
        a++;
    } while a < 15 + 1;
}


}

import "game/second-layer";

