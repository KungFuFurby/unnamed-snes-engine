

=== menu_cursor ===
    set_instrument      square
    set_volume          20

    play_note           d4 8


=== menu_select ===
    set_instrument  sawtooth

    play_note       c+4  16
    play_note       b-4  96


=== explosion_hurt_player ===
    ; ::TODO improve explosion_hurt_player sound effect::
    ;
    ; I do like the gap between the explosion and the hurt-player sound.
    ; It adds more emphasis to the player-hurt portion of the sound-effect.

    set_instrument noise
    set_volume 192

    play_note c1 4
    play_note c2 2

    set_instrument_and_adsr triangle    14 2 1 25
    set_volume 128
    play_note  f3 no_keyoff 2
    portamento c2 keyoff -25 4


    set_instrument noise
    set_volume 192
    play_note e2 4
    rest 2

    ; Explosion echo and fade-out
    set_instrument_and_adsr noise       8 2 2 18

    play_note  f1 no_keyoff 8
    portamento c1 keyoff -8 48


=== explosion_hurt_enemy ===
    set_volume 192

    set_instrument_and_gain noise       127

    ; BOOM with high pitch enemy hurt bit overpowering the boom
    play_note c1 2
    play_note e3 6
    play_note c2 2
    rest 6


    ; Explosion echo and fadeout
    set_adsr 10 2 2 18

    play_note  f1 no_keyoff 8
    portamento c1 keyoff -8 48


=== explosion ===
    set_volume 192

    set_instrument_and_gain noise       127

    ; BOOM
    ; ::TODO experiment with a meatier sample::
    play_note c1 2
    play_note c2 4
    play_note e2 4
    rest 6


    ; Explosion echo and fadeout
    set_adsr 10 2 2 18

    play_note  f1 no_keyoff 8
    portamento c1 keyoff -8 48

=== player_heal ===
    ; high pitch, same waveform as player_hurt

    set_volume 192

    set_instrument_and_adsr triangle    12 2 7 28

    play_note e5 4
    rest 4
    play_note d6 4


=== player_hurt_low_health ===
    set_volume 255

    set_instrument_and_adsr triangle    10 2 1 25

    play_note  f3 no_keyoff 4
    portamento c2 no_keyoff -25 8
    portamento c1 keyoff -90 8


    set_adsr 12 2 2 28

    start_loop 3
        rest 6

        play_note  f4 no_keyoff 4
        play_note  g5 keyoff 8
    end_loop


=== player_hurt ===

    set_volume 255

    set_instrument_and_adsr triangle    10 2 1 25

    play_note  f3 no_keyoff 4
    portamento c2 no_keyoff -25 8
    portamento c1 keyoff -90 8



=== door_slam ===
    set_volume 192

    ; ::TODO Find a way to make a SLAM sound::
    set_instrument_and_adsr sawtooth    15 1 0 21

    play_note  b2  2
    play_note  c+2 8
    play_note  e1  8


=== door_open_with_key ===
    set_volume 255

    ; key unlock
    set_instrument_and_adsr noise   15 7 3 28
    play_note  a5 6
    rest 2
    play_note  f5 8


=== door_open ===

    set_volume 192

    ; ::TODO Find a better door creaking on stone sound::
    set_instrument_and_adsr sawtooth    6 2 7 5
    play_note  e1 no_keyoff 4
    portamento c3 no_keyoff +4 16
    portamento c3 keyoff +40 8


=== jump_or_bounce ===

    set_volume 48

    set_instrument_and_adsr sawtooth    15 2 2 18

    play_note g3 no_keyoff 4
    portamento c3 no_keyoff -30 8
    portamento g3 keyoff +20 20

=== collect_item ===
    set_volume 192

    set_instrument_and_adsr triangle    12 1 4 20

    play_note  f5 no_keyoff 12
    play_note  c+5 keyoff 24


=== pickup_bomb ===

    set_volume 92

    ; ::TODO increase triangle sample volume::
    set_instrument_and_adsr triangle    12 0 7 0

    play_note d2 no_keyoff 12
    portamento c3 no_keyoff +2 16
    portamento g5 keyoff +60 20

=== throw_bomb ===

    set_volume 92

    ; ::TODO increase triangle sample volume::
    set_instrument_and_adsr triangle    10 0 7 0

    play_note g+4 no_keyoff 4
    portamento c1 no_keyoff -2 20
    portamento c1 keyoff -60 20


=== statue_spawns_enemy ===
    set_volume 127

    set_instrument_and_adsr sine        10 2 4 28

    play_note e4 no_keyoff 6
    play_note g+4 no_keyoff 6
    play_note c5 keyoff 6

    play_note e4 no_keyoff 6
    play_note g+4 no_keyoff 6
    play_note c5 keyoff 6


=== spawn_enemy_with_smoke ===
   set_volume 64

    set_instrument_and_adsr noise       10 5 5 20
    play_note f5 keyoff 4

    play_note b4 no_keyoff 8
    start_loop 6
        portamento c1 no_keyoff -15 4
        portamento c6 no_keyoff +40 4
    end_loop


=== key_spawns ===

    set_volume 128

    set_instrument_and_adsr triangle    10 6 3 12

    play_note e5  16
    play_note c+5 16
    play_note c+5 16
    rest 4
    play_note a5  16
    play_note e6  24


=== key_hits_ground ===

    set_volume 64

    set_instrument_and_adsr triangle    15 7 1 30

    play_note b6 4
    rest 16

    set_volume 48

    play_note b6 8

=== enemy_poof ===

    ; enemy hurt sfx
    ; ::TODO add more exploding/emphasis::
    set_volume 64
    set_instrument_and_adsr sawtooth    8 2 3 28
    play_note c1 no_keyoff 4
    play_note a3 no_keyoff 8
    play_note c3 keyoff 4

    rest 4

    ; smoke poof sound
    set_volume 48
    set_instrument_and_adsr noise       12 2 2 21
    play_note e+3 no_keyoff 2
    start_loop 10
        portamento c1 no_keyoff -28 4
        portamento c6 no_keyoff +30 2
    end_loop

=== enemy_hurt ===

    ; ::TODO improve (a lot)::

    ; Using sawtooth so it sounds different to player_hurt

    set_volume 64
    set_instrument_and_adsr sawtooth    8 2 3 28
    play_note c1 no_keyoff 4
    play_note a3 no_keyoff 8
    play_note c3 keyoff 4


=== sword_swing ===

    set_volume 64

    set_instrument_and_adsr noise       6 1 6 28

    play_note f+3 no_keyoff 2
    portamento c6 no_keyoff +10 5
    portamento c6 no_keyoff +40 10


=== sword_hit_shield ===

    set_volume 80

    set_instrument_and_adsr sawtooth    15 7 2 20

    play_note f6 no_keyoff 2
    portamento c6 keyoff -15 12


=== sword_hit_bolt ===

    set_volume 80

    set_instrument_and_adsr square      12 6 3 28

    play_note c6 no_keyoff 2
    portamento c4 keyoff -50 8


=== sword_hit_rook ===

    ; Sounds like an invalid-command sfx combined with a high pitch ting.
    ; Not the sound I wanted, but it conveys the "You cannot do this" feeling I am after.

    set_volume 80

    set_instrument_and_adsr sawtooth    14 7 2 20

    play_note f3    no_keyoff 2
    play_note g5    no_keyoff 2
    play_note f-3   no_keyoff 2
    play_note g5    no_keyoff 2
    play_note f--3  no_keyoff 2
    play_note f---3 no_keyoff 2

=== arrow_hit_shield ===

    set_volume 80

    set_instrument_and_adsr sawtooth    15 7 2 20

    play_note f6 no_keyoff 2
    portamento c6 keyoff -15 12


=== rook_hits_ground ===
    set_volume 255

    set_instrument_and_gain square      127

    play_note  c2 no_keyoff 2
    play_note  a1 no_keyoff 2
    play_note  g2 no_keyoff 2
    play_note  c2 no_keyoff 2
    play_note  e1 4

    rest 2

    set_gain 32

    play_note  c2 no_keyoff 2
    play_note  a1 no_keyoff 2
    play_note  g2 no_keyoff 2
    play_note  c2 no_keyoff 2
    play_note  e1 4

    rest 2

    set_gain 4

    play_note  c2 no_keyoff 1
    play_note  a1 no_keyoff 1
    play_note  g2 no_keyoff 1
    play_note  c2 no_keyoff 1
    play_note  e1 2


=== boss_shake ===
    set_volume 96

    set_instrument_and_adsr sine        13 7 5 10

    play_note  c5 no_keyoff 4
    play_note  e4 no_keyoff 4
    play_note  c5 no_keyoff 8

=== boss_rising ===
    set_volume 96

    set_instrument_and_adsr sine        7 3 0 0

    start_loop 12
        play_note  b4 no_keyoff 3
        play_note  c5 no_keyoff 3
    end_loop


=== boss_bomb_disabled ===

    set_volume 50
    set_instrument_and_adsr triangle    10 3 5 30

    play_note  d6 3
    play_note  b5 3
    rest 2

    ; fuse hiss (imagine a cartoon character extinguishing a lit fuse with their fingers)
    set_volume 192
    set_instrument_and_adsr noise       14 6 0 18

    play_note  c4 keyoff 16


=== bounce_off_wall ===
    set_volume 80

    set_instrument_and_adsr square      13 3 6 28

    play_note  b-2 no_keyoff 2
    play_note  e3 4


=== bolt_hits_wall ===
    set_volume 92

    set_instrument_and_adsr square      13 2 4 30

    play_note  c3 no_keyoff 8
    play_note  e2 4


=== crossbow_fires_bolt ===

    set_volume 160

    set_instrument_and_adsr triangle    10 6 3 28

    play_note  e-5 no_keyoff 1
    portamento g6 keyoff +90 15

