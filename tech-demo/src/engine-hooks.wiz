// Copyright (c) 2023, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "player";
import "ui";
import "joypad";
import "rng";

import "gen/enums";
import "gen/ms-drawing-functions";

import "engine/audio";
import "engine/entity_api";
import "engine/game/gamestate";
import "engine/resources/resources";

// ::TODO remove::
import "engine/game/dungeon";


namespace engine_hooks {


// WaitFrame
// =========


// Vertical blank hook.
//
// Called by `wait_frame()`.
//
// MUST NOT modify zpTmp variables.
//
// DB = 0x80
#[mem8, idx16]
inline func wait_frame__vblank__inline() {
    ui.transfer_buffer__vblank();

    metasprites.dynamic_tiles.transfer_Player_tiles__vblank__inline(player.dynamicMsFrameAddr);
}



// After Vertical-Blank hook.
//
// Called by `wait_frame()`, after the screen is re-enabled.
//
// MUST NOT modify zpTmp variables.
//
// DB = 0x80
#[mem16, idx16]
inline func wait_frame__after_vblank__mem16_idx16__inline() {
    rng.increment_rng();
}



// After Vertical-Blank hook.
//
// Called by `wait_frame()`, after the screen is re-enabled.
//
// MUST NOT modify zpTmp variables.
//
// DB = 0x80
#[mem8, idx8]
inline func wait_frame__after_vblank__mem8_idx8__inline() {
    joypad.update_state__inline();
}


// Textbox
// =======

const joypad_pressed @ &joypad.pressed : u16;
const joypad_pressed_l @ &<:joypad.pressed : u8;
const joypad_pressed_h @ &>:joypad.pressed : u8;



// GameState
// =========

typealias GameState__PlayerState : player.GameState__PlayerState;
let GameState__InitialPlayerState = player.InitialPlayerState;


// Called by `gamestate.continue_game()`.
//
// DB = 0x7e
#[mem8, idx8]
inline func gamestate__continue_game__inline() {
    // ::HACK Hardcoded checkpoint (just before the boss)::
    // ::TODO create some kind of checkpoint system::

    let LAST_HEALING_ROOM_X = 5;
    let LAST_HEALING_ROOM_Y = 7;

    x = gamestate.data.currentRoom_x;
    y = gamestate.data.currentRoom_y;

    if y >= LAST_HEALING_ROOM_Y + 1 || x >= LAST_HEALING_ROOM_X + 1 {
        // Game over before the boss
        x = resources.STARTING_ROOM_X;
        y = resources.STARTING_ROOM_Y;
    }
    else {
        // Game over after the final healing room (at the boss)
        x = LAST_HEALING_ROOM_X;
        y = LAST_HEALING_ROOM_Y;

    }

    gamestate.data.currentRoom_x = x;
    gamestate.data.currentRoom_y = y;

    gamestate.data.playerState.xPos = a = player.InitialPlayerState.xPos;
    gamestate.data.playerState.yPos = a = player.InitialPlayerState.yPos;
    gamestate.data.playerState.zPos = player.InitialPlayerState.zPos;
}



// Rest the player state from the gamestate.
//
// Called by `gamestate.continue_game()`.
//
// DB = 0x7e
#[mem8, idx8]
inline func gamestate__continue_player_gamestate__inline() {
    player.continue_player_gamestate__inline();
}


// Save the player state to the gamestate.
//
// Called by `gamestate.backup_gamestate()`.
//
// DB = 0x7e
#[mem8, idx8]
inline func gamestate__save_player_state__inline() {
    player.save_player_state__inline();
}


// Called when the key count changes.
//
// INPUT: X = nKeys
//
// DB = 0x7e
#[mem8, idx8]
let gamestate__key_count_changed = ui.draw_key_bar;



// Room Transitions
// ================


// Setup the PPU while the screen is disabled.
//
// DB = 0x80
#[mem8, idx16]
inline func load_dungeon__setup_ppu__forceblank() {
    ui.setup_ppu__forceblank();
}



// Initialize subsystems after the PPU is setup.
//
// Called after `room_transitions__setup_ppu__forceblank()`.
//
// DB = 0x7e
#[mem8, idx8]
inline func load_dungeon__init() {
    // Reset and redraw the UI
    ui.init();

    // ::TODO move into engine::
    a = dungeon.dungeonData.song;
    if a < resources.N_SONGS {
        audio.load_song_if_changed(dungeon.dungeonData.song as resources.songs);
    }
}



// Initialise the player subsystem.
//
// DB = 0x7e
#[mem8, idx8]
inline func room_transitions__reset_player__inline() {
    player.init();
}



// Reset player MetaSprites.
//
// Called when resources-over-usb2snes is active and the metasprite subsystem is reset.
//
// DB = 0x7e
#[mem8, idx8]
inline func init_player_metasprites__inline() {
    player.init_metasprites();
}



// Called after a room backup has been loaded into memory during a scrolling room transition rollback.
//
// DB = 0x7e
#[mem8, idx8]
inline func room_transitions__scrolling_room_transition_rollback__inline() {
    // Allow the player to escape any entity that may be in front of the player after a rollback.
    // Minimises the odds of an entity knocking the player back into the invalid room (immediately after rollback)
    player.zero_velocity();
    player.set_iframes_after_room_transition_rollback();
}

}

