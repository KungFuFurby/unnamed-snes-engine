// Copyright (c) 2023, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";
import "engine/registers";
import "engine/common/reset";

import "engine/audio";
import "engine/gamemodes";
import "engine/textbox";
import "engine/wait-frame";
import "engine/common/reset";
import "engine/resources/resources";

import "../joypad";

import "gen/enums";


namespace gamemodes {
namespace sound_test {


in code {

enum Options : u8 {
    PLAY_SFX,
    PLAY_SONG,
    QUIT,
};
let N_OPTIONS = 3;

in rodata0 {
    const Text = "Sound Text\n"
               ~ "  \x03 Play SFX:     \xc2\xff\n"
               ~ "  \x04 Play Song:    n/a\n"
               ~ "\n"
               ~ "  \x05 Quit\n";
}



// Sound test screen
//
// DB = 0x7e
#[mem8, idx8]
func exec() {

    wait_multiple_frames_and_fadeout();

    idx16();
    #[idx16] {
        push16(0x7e80);
        data_bank = pop8();
    // DB = 0x80

        setup_ppu__inline();

        data_bank = pop8();
    // DB = 0x7e

    }
    mem8_idx8();

    textbox.setup_textbox(5, 12);

    wait_frame_and_change_brightness(15);

    process__inline();


    set_next_game_mode(GameModes.TITLE_SCREEN);
}



// DB = 0x7e
#[mem8, idx8]
inline func process__inline() {
var _menuOption : u8 in textbox.selectedMenuItem;
var _sfxId @ &textbox.firstU16parameter : u8;

let JOYH_CONFIRM = joypad.JOYH.B | joypad.JOYH.Y | joypad.JOYH.START;

    _menuOption = 0;
    _sfxId = 0;

    while true {
        // ::TODO check if common data has changed and reload it::

        x = _menuOption;

        a = joypad.pressed_h;
        if a & JOYH_CONFIRM != 0 {
            if x == Options.PLAY_SFX as u8 {
                audio.queue_sound_effect(_sfxId as sound_effects);
            }
            else if x == Options.PLAY_SONG as u8 {
                // ::TODO implement::
            }
            else {
                break;
            }
        }
        else {
            if a & joypad.JOYH.DOWN != 0 {
                x++;
                if x >= N_OPTIONS {
                    x = 0;
                }
            }
            else if a & joypad.JOYH.UP != 0 {
                x--;
                if negative {
                    x = N_OPTIONS - 1;
                }
            }
            else if a & joypad.JOYH.LEFT_RIGHT != 0 {
                if a & joypad.JOYH.RIGHT != 0 {
                    a = 1;
                }
                else {
                    a = -1 as u8;
                }

                if x == Options.PLAY_SFX as u8 {
                    a += _sfxId;
                    if a == -1 as u8 {
                        a = N_SOUND_EFFECTS - 1;
                    }
                    if a >= N_SOUND_EFFECTS {
                        a = 0;
                    }
                    _sfxId = a;
                }
            }

            _menuOption = x;
        }

        idx16();
        #[idx16] {
            textbox.print_textbox(#:far &Text, &Text as u16);
        }
        mem8_idx8();

        wait_frame();
    }
}



// DB = 0x80
#[mem8, idx16]
inline func setup_ppu__inline() {
    // ::TODO create some kind of system for setting PPU registers::

    reset_registers();
    textbox.setup_ppu__forceblank();

    BGMODE = a = BGMODE_MODE_0;
    BG3SC = a = ((VRAM_BG3_MAP_WADDR / BGxSC_BASE_WALIGN) << BGxSC_BASE_SHIFT) | BGxSC_MAP_32x32;
    BG34NBA = a = ((VRAM_BG3_TILES_WADDR / BG34NBA_WALIGN) << BG34NBA_BG3_SHIFT);
    TM = a = TM_BG3;

    resources.load_bg_image_resource_into_vram(resources.bg_images.game_over, VRAM_BG1_MAP_WADDR, VRAM_BG1_TILES_WADDR);

    // Manually load textbox palette
    // ::TODO add palettes to the resources subsystem::
    CGADD = a = 64;

    a = 32 * 2;
    do {
        CGDATA = 0;
        a--;
    } while !zero;

    CGADD = a = 69;
    CGDATA = 0;
    CGDATA = a = 16 << 2;

    CGADD = a = 71;
    a = 0xff;
    CGDATA = a;
    CGDATA = a;

    CGADD = a = 75;
    CGDATA = a = 8;
    CGDATA = a = 31 << 2 | 1;
}

}

}
}

