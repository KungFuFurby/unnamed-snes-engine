// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "_variables";
import "_common";

import "../metatiles";
import "../metasprites";

import "gen/metasprites/dungeon";


// Entity that constantly spawns and removes itself in a (hopefully) random manner.

namespace entities {
namespace spawn_test_entity {


in wram7e {
    var globalCounter : u8;
}


let SoA_counter = SoA.state_0;


in code {

let ms_draw_function = ms.dungeon.Skull.pattern;


// Entity process routine.
//
// Called when the entity is spawned
//
// DB = 0x7e
#[mem8, idx8]
func init(entityId : u8 in x) {
    mem16();
    #[mem16] {
        SoA.xVelocity[unaligned x] = aa =  0x0120;
        SoA.yVelocity[unaligned x] = aa = -0x0120 as u16;

        SoA.metaSpriteFrame[unaligned x] = aa = &ms.dungeon.Skull.frames[0] as u16;

        SoA_counter[unaligned x] = aa = 10 * 60;
    }
    mem8();
}


// Entity process routine.
//
// Called once per frame.
//
// DB = 0x7e
#[mem8, idx8]
func process(entityId : u8 in x) : bool in carry {
    metatiles.move_entity_and_collision_test(x);

    a = SoA.movementState[x];
    if a & MovementState.X_AXIS_COLLISION != 0 {
        mem16();
        #[mem16] {
            SoA.xVelocity[unaligned x] = aa = 0 - SoA.xVelocity[unaligned x];
        }
        mem8();
    }

    a = SoA.movementState[x];
    if a & MovementState.Y_AXIS_COLLISION != 0 {
        mem16();
        #[mem16] {
            SoA.yVelocity[unaligned x] = aa = 0 - SoA.yVelocity[unaligned x];
        }
        mem8();
    }


    globalCounter++;
    a = globalCounter;

    // 3 random primes, should make the spawning/deleting more unpredictable
    if a == 79 || a == 149 || a == 211 {
        push8(x);
        entities.spawn_entity(a = 128, a = 112, a = 0);
        x = pop8();
    }
    else if a == 251 {
        globalCounter = 0;

        return false;
    }

    // Remove entity when `SoA_counter` is zero.
    {
        mem16();
        #[mem16] {
            SoA_counter[unaligned x]--;
        }
        mem8();

        if zero {
            return false;
        }
    }

    return true;
}



}

}
}

