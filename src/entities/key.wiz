// Copyright (c) 2022, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "_variables";
import "../player";
import "../gamestate";

namespace entities {

// NOTE: The key entity uses the tile hitbox to preform an aabb-point player-key collision test
namespace key {

in wram7e {

// The gamestate flag id for the key
var SoA_gameFlag : [ u8 ; SoA.BA_SIZE ] in SoA.state_0_l;

}


in code {


// Entity init routine.
//
// Called when the entity is spawned.
//
// Parameter: game flag for the key
//
// DB = 0x7e
#[mem8, idx8]
func init(entityId : u8 in y, parameter : u8 in x) {

    SoA_gameFlag[y] = a = x;

    zero = gamestate.is_flag_clear__keep_y(x);
    if !zero {
        // Key has already been collected
        SoA.health[y] = a = 0;
    }
}




// Entity process routine
//
// Called once per frame.
//
// DB = 0x7e
#[mem8, idx8]
func process(entityId : u8 in y) {
    a = SoA.health[y];
    if !zero {
        // Key has not been collected

        a = SoA.zPos[y];
        if !zero {
            // Key is not on the ground
            // Drop key until it hits the ground
            a--;
            SoA.zPos[y] = a;
        }
        else {
            // Key is on the ground (and can be collected)
            // Test if the player's position is inside the tile hitbox
            a = SoA.xPos_px[y] - SoA.tileHitbox_halfWidth[y];
            if !carry { a = 0; }
            if a < player.xPos.px {

                a = SoA.xPos_px[y] + SoA.tileHitbox_halfWidth[y];
                if carry { a = 0xff; }
                if a >= player.xPos.px {

                    a = SoA.yPos_px[y] - SoA.tileHitbox_halfHeight[y];
                    if !carry { a = 0; }
                    if a < player.yPos.px {

                        a = SoA.yPos_px[y] + SoA.tileHitbox_halfHeight[y];
                        if carry { a = 0xff; }
                        if a >= player.yPos.px {
                            // Player has touched the key

                            // despawn key
                            x = y;
                            SoA.health[x] = 0;

                            gamestate.set_game_flag(SoA_gameFlag[y]);

                            // Y is not entityId.
                            gamestate.increment_key_count();

                            // ::TODO collected key sound effect::
                            // ::TODO spawn collected key particle::

                            return;
                        }
                    }
                }
            }
        }
    }
}

}


}
}

