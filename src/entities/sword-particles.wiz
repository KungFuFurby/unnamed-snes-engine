// Copyright (c) 2022, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "_variables";
import "_base";
import "../common/math";

import "../../gen/entities";


// ::TODO add a generic particle entity::


namespace entities {
namespace sword_particles {

let SoA_animationCounter = SoA.state_0_l;


let ANIMATION_DELAY    = 6;
let N_ANIMATION_FRAMES = 3;


in code {


// Entity init routine.
//
// Called when the entity is spawned
//
// DB = 0x7e
#[mem8, idx8]
func init(entityId : u8 in y, parameter : u8 in x) {

    SoA.yPos_px[y] = a = SoA.yPos_px[y] + SoA.zPos[y];

    SoA.metaSpriteFrame[y] = a = 0;
    SoA_animationCounter[y] = a = ANIMATION_DELAY;
}



// Entity process routine
//
// Called once per frame.
//
// DB = 0x7e
#[mem8, idx8]
func process(entityId : u8 in y) : bool in carry {

    a = SoA_animationCounter[y];
    if !zero {
        a--;
        SoA_animationCounter[y] = a;
    }
    else {
        SoA_animationCounter[y] = a = ANIMATION_DELAY;

        a = SoA.metaSpriteFrame[y];
        a++;
        if a >= N_ANIMATION_FRAMES {
            return false;
        }
        SoA.metaSpriteFrame[y] = a;
    }

    return true;
}

}


}
}

