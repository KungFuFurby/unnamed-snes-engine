// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "../../gen/entities";

import "_variables";

import "../player";
import "../metasprites";


namespace entities {


in zeropage {
    var __currentEntityTablePos : u8;
}


in code {


// Initialize the entity subsystem
//
// DB = 0x7e
#[mem8, idx8]
func init() {
    mem16();
    #[mem16] {
        // Set player's process function to a blank function.
        // Required as the player is processed before `process_active_entities()` is called.
        aa = _blank_player_function as u16;
        entities.SoA.process_function[0] = aa as func(u8 in x) : bool in carry;

        // Set player's ms_draw function
        aa = player.ms_draw_function as u16;
        entities.SoA.ms_draw_function[0] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);

        entities.SoA.metaSpriteFrameTable[0] = aa = metasprites.MsFsData.frameTable[unaligned player.MS_FS_DATA_ADDR];
        entities.SoA.msAnimationTable[0] = aa = metasprites.MsFsData.animationTable[unaligned player.MS_FS_DATA_ADDR];

        {
            // ::TODO cleanup player init, this code should not be here::

            metasprites.init_player_metasprite_animation__inline();
            mem8_idx8();
            #[mem8, idx8] {
                metasprites.init_entity_metasprite_animation__after_init(PLAYER_ENTITY_ID);
            }
            mem16();
        }


        // Set all process_functions to blank (except player)
        aa = _blank_entity_function as u16;

        x = entities.SoA.LAST_INDEX;
        do {
            // Reset process function
            entities.SoA.process_function[unaligned x] = aa as func(u8 in x) : bool in carry;
            entities.SoA.ms_draw_function[unaligned x] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);

            // Clear entity shadow
            (&entities.SoA._shadowSizeAndBlank as *u16)[unaligned x] = 0;

            x--;
            x--;
        } while !zero;
    }
    mem8();


    // Populate `activeEntitiesTable`
    {
        numberOfActiveEntities = a = 1;

        a = SoA.LAST_INDEX;
        x = ACTIVE_ENTITIES_TABLE__LAST_POS;

        do {
            activeEntitiesTable[x] = a;

            a--;
            a--;

            x--;
        }
        while !negative;
    }
}



// Spawn entity
//
// NOTE: This function may fail if `entityType` is invalid or there are no more unused entityIds.
//
// DB = 0x7e
#[mem8, idx8]
func spawn_entity(xPos : u8 in zpTmp_byte_0, yPos : u8 in zpTmp_byte_1, entityType : u8 in a, initParameter : u8 in y) {
var _init_function_u16 : u16 in zpTmp_word_0;

    ^if a < N_ENTITY_TYPES {

        // Retrieve the next unused entity from the `activeEntitiesTable` (if one is available)
        x = numberOfActiveEntities;
        ^if x < sizeof(typeof(activeEntitiesTable)) {
            // This code path MUST spawn an entity
            push8(y);

            y = activeEntitiesTable[x];
            // Y = entityId

            // mark entity active
            numberOfActiveEntities = ++x;

            x = a = a <<< 1;

            mem16();
            #[mem16] {
                _init_function_u16 = aa = (entity_rom_data.init_functions as far *u16)[unaligned x];

                (&entities.SoA.process_function[0] as *u16)[unaligned y] = aa = (entity_rom_data.process_functions as far *u16)[unaligned x];
                (&entities.SoA._visionAB[0] as *u16)[unaligned y] = aa = entity_rom_data.vision_ab[unaligned x];

                (&entities.SoA._zPosAndBlank[0] as *u16)[unaligned y] = aa = entity_rom_data.initial_zpos_and_blank[unaligned x];

                (&entities.SoA._healthAndAttackPower[0] as *u16)[unaligned y] = aa = entity_rom_data.health_and_attack_power[unaligned x];

                aa = (entity_rom_data.metasprite_framesets as far *u16)[unaligned x];
            }

            mem16_idx16();
            #[mem16, idx16] {
                // MetaSprite Data
                xx = aa;

                (&entities.SoA._shadowSizeAndBlank[0] as *u16)[unaligned yy] = aa = metasprites.MsFsData.shadowSize__u16[unaligned xx] & 0xff;
                (&entities.SoA.tileHitbox[0].first as *u16)[unaligned yy] = aa = metasprites.MsFsData.tileHitbox[unaligned xx];

                (&<:entities.SoA.ms_draw_function[0] as *u16)[unaligned yy] = aa = metasprites.MsFsData.drawFunction[unaligned xx];
                (&<:entities.SoA.metaSpriteFrameTable[0] as *u16)[unaligned yy] = aa = metasprites.MsFsData.frameTable[unaligned xx];
                (&<:entities.SoA.msAnimationTable[0] as *u16)[unaligned yy] = aa = metasprites.MsFsData.animationTable[unaligned xx];
            }
            idx8();

            #[mem16, idx8] {
                metasprites.init_entity_metasprite_animation__inline(y);

                // Clear unused data
                // Required: No `stz.w addr,y` instruction.
                x = y;

                entities.SoA.xVelocity[unaligned x] = 0;
                entities.SoA.yVelocity[unaligned x] = 0;

                (&entities.SoA._movementStateAndIframeCounter as *u16)[unaligned x] = 0;

                entities.SoA.state_0[unaligned x] = 0;
                entities.SoA.state_1[unaligned x] = 0;
                entities.SoA.state_2[unaligned x] = 0;
                entities.SoA.state_3[unaligned x] = 0;
                entities.SoA.state_4[unaligned x] = 0;
                entities.SoA.state_5[unaligned x] = 0;
                entities.SoA.state_6[unaligned x] = 0;
                entities.SoA.state_7[unaligned x] = 0;
            }
            mem8_idx8();

            // x & y = entityId

            a = xPos;
            entities.SoA.previousXpos[x] = a;
            entities.SoA.xPos_px[x] = a;
            entities.SoA.xPos_sx[x] = 0;

            a = yPos;
            entities.SoA.previousYpos[x] = a;
            entities.SoA.yPos_px[x] = a;
            entities.SoA.yPos_sx[x] = 0;

            // Call spawn function
            x = pop8();

            // Push entityId onto stack.
            // Y is NOT preserved by init function and the init function may call `spawn_entity`
            push8(y);
            __spawn_entity__call_init_function_ptr(y, x, _init_function_u16);

            y = pop8();
            ^return metasprites.init_entity_metasprite_animation__after_init(y);
        }
    }
}



// Call the entity's init function (which is located in the temporary variable `zpTmp_word_0`)
//
// MUST ONLY BE CALLED BY `spawn_entity()`
//
// DB = 0x7e
#[mem8, idx8]
func __spawn_entity__call_init_function_ptr(entityId : u8 in y, parameter : u8 in x, init_function_u16 : u16 in zpTmp_word_0) {
var _init_function_ptr : func(u8 in y, u8 in x) in init_function_u16;

    return (*&_init_function_ptr)(y, x);
}



// Execute the process routine in all active entities
//
// DB = 0x7e
#[mem8, idx8]
inline func process_active_entities()
{
    x = numberOfActiveEntities;
    if !zero {
        do {
            x--;
            __currentEntityTablePos = x;

            y = activeEntitiesTable[x];
            currentEntityId = y;
            x = y;
            carry = entities.SoA.process_function[unaligned x](y);
            if !carry {
                // Entity no longer active


                // Remove entity from active entities list, preserving active entity order.
                //
                // Preserving entity order should be friendlier to `sort_active_entities()`.
                numberOfActiveEntities--;

                y = currentEntityId;

                mem16();
                #[mem16] {
                    // ASSUMES numberOfActiveEntities <= ACTIVE_ENTITIES_TABLE__LAST_POS
                    // ASSUMES numberOfActiveEntities < sizeof(activeEntitiesTable)

                    // Must not change Y

                    x = __currentEntityTablePos;
                    if x < numberOfActiveEntities {
                        do {
                            // Swap activeEntitiesTable[x] and activeEntitiesTable[x + 1]
                            aa = (&activeEntitiesTable as *u16)[unaligned x];
                            swap_bytes(aa);
                            (&activeEntitiesTable as *u16)[unaligned x] = aa;

                            x++;
                        } while x < numberOfActiveEntities;
                    }


                    // Set process_function and ms_draw_function to a blank function (just in case they are accidentally called)
                    aa = _blank_entity_function as u16;
                    entities.SoA.process_function[unaligned y] = aa as func(u8 in x) : bool in carry;
                    entities.SoA.ms_draw_function[unaligned y] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);
                }
                mem8();


                // Do not draw a shadow on unused entities.
                //
                // Y is currentEntityId;
                a = metasprites.ShadowSize.NONE as u8;
                SoA.shadowSize[unaligned y] = a as metasprites.ShadowSize;
            }
            else {
                // entity is still alive, process entity animation
                //
                // The `!zero` test prevents the player's animations from being executed twice in single display frame.
                // This is required as the player's hitboxes need to be calculated AFTER the player's animations have been process and BEFORE this function is called.
                y = currentEntityId;
                if !zero {
                    metasprites.process_entity_animation(y);
                }
            }

            x = __currentEntityTablePos;
        } while !zero;
    }
}



// Execute the ms_draw_function for all active entities (excluding player)
//
// DB = 0x7e
#[mem8, idx8]
inline func draw_active_entities()
{
    x = 0;
    if x < numberOfActiveEntities {
        do {
            __currentEntityTablePos = x;

            metasprites._draw_entity__inline(y = activeEntitiesTable[x]);

            x = __currentEntityTablePos;
            x++;
        } while x < numberOfActiveEntities;
    }
}



// Sort the active entities by their Y position.
//
// This function is not inline so I can measure its CPU usage with Mesen-S.
//
// Insertion sort.
//
// DB = 0x7e
#[mem8, idx8]
func sort_active_entities()
{
    // Thanks Wikipedia for the Insertion sort algorithm.
    //
    // "Insertion sort."  Wikipedia, The Free Encyclopedia.
    // https://en.wikipedia.org/wiki/Insertion_sort#Algorithm
    // (accessed October 16, 2021).


var _entityId      : u8 in zpTmp_byte_0;
var _yPosToCompare : u8 in zpTmp_byte_1;


    // A = yPos to compare
    // X = index into activeEntitiesTable
    // Y = index into entities SoA


    x = 1;
    if x < numberOfActiveEntities {
        do {
            y = activeEntitiesTable[x];
            a = entities.SoA.yPos_px[y];

            _entityId = y;

            y = (&activeEntitiesTable[-1])[x];
            if a < entities.SoA.yPos_px[y] {
                __currentEntityTablePos = x;

                _yPosToCompare = a;

                do {
                    // ::ANNOY no `sty addr,x` instruction on the 65816::
                    // If I could use `sty addr,x` then I could remove `_yPosToCompare` and store it in A.
                    activeEntitiesTable[x] = a = y;

                    x--;
                    break if zero;

                    a = _yPosToCompare;

                    y = (&activeEntitiesTable[-1])[x];
                } while a < entities.SoA.yPos_px[y];


                activeEntitiesTable[x] = a = _entityId;

                x = __currentEntityTablePos;
            }

            x++;
        } while x < numberOfActiveEntities;
    }
}


// A blank init process function that does nothing
//
// Used to determine if `entity_rom_data` is blank
//
// DB = 0x7e
func _blank_init_function(entityId : u8 in y, parameter : u8 in x) {
}


// A blank entity process function that returns false (entity no-longer active)
//
// DB = 0x7e
func _blank_entity_function(entityId : u8 in y) : bool in carry {
    return false;
}


// A special blank entity process function that return true (entity is always active)
//
// Used by entities that are processed outside of `process_active_entities()`.
//
// DB = 0x7e
func _blank_player_function() : bool in carry {
    return true;
}


}

}


// Entity ROM Data is populated by the `tools/insert_resources.py` and `tools/_entity_data.py` scripts
namespace entity_rom_data {

let N_ENTRIES = entities.N_ENTITY_TYPES;

// Filling the data with blank functions and `0xaa` bytes.
// This is used by the *insert_resources* script, to ensure the entity_rom_data is inserted into the correct location.
in rodata0 {
    const __init_funtions : [ func(u8 in y, u8 in x) ; N_ENTRIES ] = [ entities._blank_init_function ; N_ENTRIES ];
    let init_functions = far &__init_funtions;

    const __process_funtions : [ func(u8 in y) : bool in carry ; N_ENTRIES ] = [ entities._blank_entity_function ; N_ENTRIES ];
    let process_functions = far &__process_funtions;

    const __metasprite_framesets : [ *const metasprites.MsFramesetFormat ; N_ENTRIES ] = [ 0xaaaa as *metasprites.MsFramesetFormat ; N_ENTRIES ];
    let metasprite_framesets = far &__metasprite_framesets;

    const __initial_zpos_and_blank : [u8 ; N_ENTRIES * 2] = [ 0xaa ; N_ENTRIES * 2];
    let initial_zpos_and_blank = far &__initial_zpos_and_blank as far *u16;

    const __vision_ab : [u8 ; N_ENTRIES * 2] = [ 0xaa ; N_ENTRIES * 2];
    let vision_ab = far &__vision_ab as far *u16;

    const __health_and_attack_power : [u8 ; N_ENTRIES * 2] = [ 0xaa ; N_ENTRIES * 2];
    let health_and_attack_power = far &__health_and_attack_power as far *u16;
}

}

