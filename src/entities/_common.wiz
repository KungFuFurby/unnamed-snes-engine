// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "../../gen/entity-data";

import "../metasprites";
import "_variables";
import "player";


namespace entities {

let N_ENTITY_TYPES = sizeof(typeof(entities.entity_data.process_function_l));


in zeropage {
    var __currentEntityTablePos : u8;
}


in code {


// Initialize the entity subsystem
//
// DB = 0x7e
#[mem8, idx8]
func init() {
    mem16();
    #[mem16] {
        // Set player's process function to a blank function.
        // Required as the player is processed before `process_active_entities()` is called.
        aa = _blank_player_function as u16;
        entities.SoA.process_function[0] = aa as func(u8 in x) : bool in carry;

        // Set player's ms_draw function
        aa = entities.player.ms_draw_function as u16;
        entities.SoA.ms_draw_function[0] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);

        entities.SoA.metaSpriteFrameTable[0] = aa = &entities.player.ms_frame_table as u16;


        // Set all process_functions to blank (except player)
        aa = _blank_entity_function as u16;

        x = entities.SoA.LAST_INDEX;
        do {
            // Reset process function
            entities.SoA.process_function[unaligned x] = aa as func(u8 in x) : bool in carry;
            entities.SoA.ms_draw_function[unaligned x] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);

            // Clear entity shadow
            (&entities.SoA._zPosAndShadow as *u16)[unaligned x] = 0;

            x--;
            x--;
        } while !zero;
    }
    mem8();


    // Populate `activeEntitiesTable`
    {
        numberOfActiveEntities = a = 1;

        a = SoA.LAST_INDEX;
        x = ACTIVE_ENTITIES_TABLE__LAST_POS;

        do {
            activeEntitiesTable[x] = a;

            a--;
            a--;

            x--;
        }
        while !negative;
    }
}



// Spawn entity
//
// NOTE: This function may fail if `entityType` is invalid or there are no more unused entityIds.
//
// DB = 0x7e
#[mem8, idx8]
func spawn_entity(xPos : u8 in <:dpTmp0, yPos : u8 in >:dpTmp0, entityType : u8 in a, initParameter : u8 in y) {
let _init_function_u16 = dpTmp1;
let _init_function_ptr = _init_function_u16 as func(u8 in x, u8 in y);

    ^if a < N_ENTITY_TYPES {

        // Retrieve the next unused entity from the `activeEntitiesTable` (if one is available)
        x = numberOfActiveEntities;
        ^if x < sizeof(typeof(activeEntitiesTable)) {
            // This code path MUST spawn an entity
            push8(y);

            y = activeEntitiesTable[x];
            // mark entity active
            numberOfActiveEntities = ++x;


            x = a;

            // Y = entityId
            // X = entity Data Index

            (&<:entities.SoA.process_function[0] as *u8)[y] = a = (far &entities.entity_data.process_function_l as far *u8)[x];
            (&>:entities.SoA.process_function[0] as *u8)[y] = a = (far &entities.entity_data.process_function_h as far *u8)[x];

            (&<:entities.SoA.ms_draw_function[0] as *u8)[y] = a = (far &entities.entity_data.ms_draw_function_l as far *u8)[x];
            (&>:entities.SoA.ms_draw_function[0] as *u8)[y] = a = (far &entities.entity_data.ms_draw_function_h as far *u8)[x];

            (&<:entities.SoA.metaSpriteFrameTable[0] as *u8)[y] = a = (far &entities.entity_data.ms_frame_table_l as far *u8)[x];
            (&>:entities.SoA.metaSpriteFrameTable[0] as *u8)[y] = a = (far &entities.entity_data.ms_frame_table_h as far *u8)[x];

            a = (far &entities.entity_data.shadow_size as far *u8)[x];
            entities.SoA.shadowSize[y] = a as entities.ShadowSize;

            entities.SoA.zPos[y] = a = (far &entities.entity_data.initial_zpos as far *u8)[x];
            entities.SoA.tileHitbox_halfWidth[y] = a = (far &entities.entity_data.tile_hitbox_half_width as far *u8)[x];
            entities.SoA.tileHitbox_halfHeight[y] = a = (far &entities.entity_data.tile_hitbox_half_height as far *u8)[x];

            <:_init_function_u16 = a = (far &entities.entity_data.init_function_l as far *u8)[x];
            >:_init_function_u16 = a = (far &entities.entity_data.init_function_h as far *u8)[x];


            // Clear unused data
            x = y;

            entities.SoA.xPos_sx[x] = 0;
            entities.SoA.xPos_px[x] = a = xPos;
            entities.SoA.yPos_sx[x] = 0;
            entities.SoA.yPos_px[x] = a = yPos;

            mem16();
            #[mem16] {
                entities.SoA.xVelocity[unaligned x] = 0;
                entities.SoA.yVelocity[unaligned x] = 0;

                entities.SoA._movementStateAndBlank[unaligned x] = 0;
                (&entities.SoA._healthAndIframeCounter as *u16)[unaligned x] = 0;

                entities.SoA.state_0[unaligned x] = 0;
                entities.SoA.state_1[unaligned x] = 0;
                entities.SoA.state_2[unaligned x] = 0;
                entities.SoA.state_3[unaligned x] = 0;
                entities.SoA.state_4[unaligned x] = 0;
                entities.SoA.state_5[unaligned x] = 0;
                entities.SoA.state_6[unaligned x] = 0;
                entities.SoA.state_7[unaligned x] = 0;
            }
            mem8();


            // Call spawn function
            y = pop8();
            return _init_function_ptr(x, y);
        }
    }
}



// Execute the process routine in all active entities
//
// DB = 0x7e
#[mem8, idx8]
inline func process_active_entities()
{
let _tmp = <:dpTmp0;

    y = numberOfActiveEntities;
    if !zero {
        do {
            y--;
            __currentEntityTablePos = y;

            x = activeEntitiesTable[y];
            currentEntityId = x;
            carry = entities.SoA.process_function[unaligned x](x);
            if !carry {
                // Entity no longer active


                // Remove entity from active entities list, preserving active entity order.
                //
                // Preserving entity order should be friendlier to `sort_active_entities()`.
                numberOfActiveEntities--;

                mem16();
                #[mem16] {
                    // ASSUMES numberOfActiveEntities <= ACTIVE_ENTITIES_TABLE__LAST_POS
                    // ASSUMES numberOfActiveEntities < sizeof(activeEntitiesTable)

                    x = __currentEntityTablePos;
                    if x < numberOfActiveEntities {
                        do {
                            // Swap activeEntitiesTable[x] and activeEntitiesTable[x + 1]
                            aa = (&activeEntitiesTable as *u16)[unaligned x];
                            swap_bytes(aa);
                            (&activeEntitiesTable as *u16)[unaligned x] = aa;

                            x++;
                        } while x < numberOfActiveEntities;
                    }


                    // Set process_function and ms_draw_function to a blank function (just in case they are accidentally called)
                    x = currentEntityId;
                    aa = _blank_entity_function as u16;
                    entities.SoA.process_function[unaligned x] = aa as func(u8 in x) : bool in carry;
                    entities.SoA.ms_draw_function[unaligned x] = aa as func(u16 in yy, u16 in metasprites.xPos, u16 in metasprites.yPos);
                }
                mem8();


                // Do not draw a shadow on unused entities.
                //
                // X is currentEntityId;
                SoA.shadowSize[unaligned x] = ShadowSize.NONE;
            }

            y = __currentEntityTablePos;
        } while !zero;
    }
}



// Execute the ms_draw_function for all active entities (excluding player)
//
// DB = 0x7e
#[mem8, idx8]
inline func draw_active_entities()
{
    x = 0;
    if x < numberOfActiveEntities {
        do {
            __currentEntityTablePos = x;

            metasprites._draw_entity__inline(y = activeEntitiesTable[x]);

            x = __currentEntityTablePos;
            x++;
        } while x < numberOfActiveEntities;
    }
}



// Sort the active entities by their Y position.
//
// This function is not inline so I can measure its CPU usage with Mesen-S.
//
// Insertion sort.
//
// DB = 0x7e
#[mem8, idx8]
func sort_active_entities()
{
    // Thanks Wikipedia for the Insertion sort algorithm.
    //
    // "Insertion sort."  Wikipedia, The Free Encyclopedia.
    // https://en.wikipedia.org/wiki/Insertion_sort#Algorithm
    // (accessed October 16, 2021).


let _entityId = >:dpTmp0;
let _yPosToCompare = <:dpTmp0;


    // A = yPos to compare
    // X = index into activeEntitiesTable
    // Y = index into entities SoA


    x = 1;
    if x < numberOfActiveEntities {
        do {
            y = activeEntitiesTable[x];
            a = entities.SoA.yPos_px[y];

            _entityId = y;

            y = (&activeEntitiesTable[-1])[x];
            if a < entities.SoA.yPos_px[y] {
                __currentEntityTablePos = x;

                _yPosToCompare = a;

                do {
                    // ::ANNOY no `sty addr,x` instruction on the 65816::
                    // If I could use `sty addr,x` then I could remove `_yPosToCompare` and store it in A.
                    activeEntitiesTable[x] = a = y;

                    x--;
                    break if zero;

                    a = _yPosToCompare;

                    y = (&activeEntitiesTable[-1])[x];
                } while a < entities.SoA.yPos_px[y];


                activeEntitiesTable[x] = a = _entityId;

                x = __currentEntityTablePos;
            }

            x++;
        } while x < numberOfActiveEntities;
    }
}



// A blank entity process function that returns false (entity no-longer active)
//
// DB = 0x7e
func _blank_entity_function() : bool in carry {
    return false;
}


// A special blank entity process function that return true (entity is always active)
//
// Used by entities that are processed outside of `process_active_entities()`.
//
// DB = 0x7e
func _blank_player_function() : bool in carry {
    return true;
}



// COLLISIONS
// ==========


// Update player's collision boxes
//
// DB = 0x7e
#[mem8, idx8]
inline func update_player_collision_boxes() {
    _update_player_hurtbox();
    player.update_hitbox();
}



// Update `playerHurtbox`.
//
// DB = 0x7e
#[mem8, idx8]
inline func _update_player_hurtbox() {
    a = player.xPos.px - player.HURTBOX_LEFT_OFFSET;
    if !carry {
        a = 0;
    }
    playerHurtbox.left = a;

    a = player.xPos.px + player.HURTBOX_RIGHT_OFFSET;
    if carry {
        a = 0xff;
    }
    playerHurtbox.right = a;

    a = player.yPos.px - player.HURTBOX_TOP_OFFSET;
    if !carry {
        a = 0;
    }
    playerHurtbox.top = a;

    a = player.yPos.px + player.HURTBOX_BOTTOM_OFFSET;
    if carry {
        a = 0xff;
    }
    playerHurtbox.bottom = a;
}



// Test for a collision between an entity's tile-hitbox and the player's hurtbox.
//
// If there is a collision, the `entities.player.hurtbox_collided_with_entity__inline()`
// function will be called.
//
// ASSUMES `entityId` is not player.
//
// Return: true if there is a collision
// Return: X = currentEntityId
//
// DB = 0x7e
#[mem8, idx8]
func test_tile_hitbox_player_hurtbox(entityId : u8 in x) : bool in carry {

    // calculate entity tileHitbox left
    a = entities.SoA.xPos_px[x] - entities.SoA.tileHitbox_halfWidth[x];
    if a < playerHurtbox.right {

        // calculate entity tileHitbox right
        a = entities.SoA.xPos_px[x] + entities.SoA.tileHitbox_halfWidth[x];
        if a >= playerHurtbox.left {

            // calculate entity tileHitbox top
            a = entities.SoA.yPos_px[x] - entities.SoA.tileHitbox_halfHeight[x];
            if a < playerHurtbox.bottom {

                // calculate entity tileHitbox bottom
                a = entities.SoA.yPos_px[x] + entities.SoA.tileHitbox_halfHeight[x];
                if a >= playerHurtbox.bottom {

                    entities.player.hurtbox_collided_with_entity__inline(x);

                    x = currentEntityId;

                    return true;
                }
            }
        }
    }

    return false;
}



// Test for a collision between an entity's tile-hitbox and the player's hitbox.
//
// If there is a collision, the `entities.player.hitbox_collided_with_entity__inline()`
// function will be called.
//
// ASSUMES `entityId` is not player.
//
// Return: true if there is a collision
// Return: X = currentEntityId
//
// DB = 0x7e
#[mem8, idx8]
func test_tilebox__player_hitbox(entityId : u8 in x) : bool in carry {

    // calculate entity tileHitbox left
    a = entities.SoA.xPos_px[x] - entities.SoA.tileHitbox_halfWidth[x];
    if a < playerHitbox.right {

        // calculate entity tileHitbox right
        a = entities.SoA.xPos_px[x] + entities.SoA.tileHitbox_halfWidth[x];
        if a >= playerHitbox.left {

            // calculate entity tileHitbox top
            a = entities.SoA.yPos_px[x] - entities.SoA.tileHitbox_halfHeight[x];
            if a < playerHitbox.bottom {

                // calculate entity tileHitbox bottom
                a = entities.SoA.yPos_px[x] + entities.SoA.tileHitbox_halfHeight[x];
                if a >= playerHitbox.bottom {

                    entities.player.hitbox_collided_with_entity__inline(x);

                    x = currentEntityId;

                    return true;
                }
            }
        }
    }

    return false;
}


}

}

