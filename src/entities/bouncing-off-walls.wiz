// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";

import "_variables";
import "_base";
import "../common/math";

import "../../gen/entities";


namespace entities {
namespace bouncing_off_walls {


let SoA_animationCounter = SoA.state_0_l;


let SPEED          = 0x0120;
let SPEED_INCREASE = 0x0085;
let MAX_SPEED      = 0x0380;


let ANIMATION_DELAY = 4;
let ANIMATION_FRAME_MASK = 0x7;


in code {


// Entity process routine.
//
// Called when the entity is spawned
//
// DB = 0x7e
#[mem8, idx8]
func init(entityId : u8 in x, parameter : u8 in y) {
    mem16();
    #[mem16] {
        // Set starting velocity depending on `parameter`

        aa = 0;
        if y < init_parameter.up as u8 || y == init_parameter.down_right as u8 {
            aa = SPEED;
        }
        else if y >= init_parameter.up_left as u8 && y < init_parameter.down as u8 {
            aa = -SPEED as u16;
        }
        SoA.xVelocity[unaligned x] = aa;

        aa = 0;
        if y >= init_parameter.down_left as u8 {
            aa = SPEED;
        }
        else if y >= init_parameter.up_right as u8 && y < init_parameter.left as u8 {
            aa = -SPEED as u16;
        }
        SoA.yVelocity[unaligned x] = aa;
    }
    mem8();

    SoA.metaSpriteFrame[unaligned x] = ms_frames.frame0;
}



// Entity process routine.
//
// Called once per frame.
//
// DB = 0x7e
#[mem8, idx8]
func process(entityId : u8 in x) : bool in carry {

    x = base.process_collisions(x);


    // React to colliding with player's weapon
    if collisionState $ CollisionState.PLAYER_WEAPON_BIT {
        collided_with_player_hitbox(x);
    }


    // React to colliding with player
    if collisionState $ CollisionState.PLAYER_HURTBOX_BIT {
        collided_with_player_hurtbox(x);
    }


    // Change velocity on a tile collision

    a = SoA.movementState[x];
    if a & MovementState.X_AXIS_COLLISION != 0 {
        mem16();
        #[mem16] {
            SoA.xVelocity[unaligned x] = aa = 0 - SoA.xVelocity[unaligned x];
        }
        mem8();
    }

    a = SoA.movementState[x];
    if a & MovementState.Y_AXIS_COLLISION != 0 {
        mem16();
        #[mem16] {
            SoA.yVelocity[unaligned x] = aa = 0 - SoA.yVelocity[unaligned x];
        }
        mem8();
    }


    // Process animation
    SoA_animationCounter[x]--;
    if negative {
        SoA_animationCounter[x] = a = ANIMATION_DELAY - 1;

        // Increment animation Frame
        a = SoA.metaSpriteFrame[x];
        a++;
        SoA.metaSpriteFrame[x] = a = a & ANIMATION_FRAME_MASK;
    }


    return true;
}



// Reflect the entity when it collides with the player's sword.
//
// The collision centre is used to determine the new direction of the entity.
//
// ASSUMES: `collisionCentre` is valid.
//
// KEEP: X
//
// DB = 0x7e
#[mem8, idx8]
inline func collided_with_player_hitbox(entityId : u8 in x) {
    mem16();
    #[mem16] {
        aa = SoA.xVelocity[unaligned x];
        if negative {
            aa = negate16(aa);
        }
        aa += SPEED_INCREASE;
        if aa >= MAX_SPEED + 1 {
            aa = MAX_SPEED;
        }

        y = SoA.xPos_px[unaligned x];
        if y < base.collisionCentre_x {
            aa = negate16(aa);
        }
        SoA.xVelocity[unaligned x] = aa;


        aa = SoA.yVelocity[unaligned x];
        if negative {
            aa = negate16(aa);
        }
        aa += SPEED_INCREASE;
        if aa >= MAX_SPEED + 1 {
            aa = MAX_SPEED;
        }

        y = SoA.yPos_px[unaligned x];
        if y < base.collisionCentre_y {
            aa = negate16(aa);
        }
        SoA.yVelocity[unaligned x] = aa;
    }
    mem8();
}



// Bounce back when colliding with player body.
//
// KEEP: x
//
// DB = 0x
inline func collided_with_player_hurtbox(entityId : u8 in x) {
    mem16();
    #[mem16] {
        SoA.xVelocity[unaligned x] = aa = 0 - SoA.xVelocity[unaligned x];
        SoA.yVelocity[unaligned x] = aa = 0 - SoA.yVelocity[unaligned x];
    }
    mem8();
}

}

}
}

