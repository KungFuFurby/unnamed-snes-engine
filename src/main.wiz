// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "memmap";
import "../wiz/common/snes/snes";

import "common/reset";
import "common/dma";

import "gameloop";
import "gamemodes";
import "metatiles";
import "metasprites";
import "resources";


in code {


// DB = 0x80
#[fallthrough, mem8, idx16]
func main() {
    snes.ppu.brightness = a = snes.ppu.BRIGHTNESS_FORCE_BLANK;


    metasprites.setup__fblank();

    snes.ppu.irq_enable = a = snes.ppu.IRQ_ENABLE_AUTOJOY | snes.ppu.IRQ_ENABLE_NMI;


    push8(a = 0x7e);
    data_bank = pop8();
// DB = 0x7e

    idx8();
    #[idx8] {
        gameloop.init();


        while true {
            a = gameMode as u8;
            if a >= N_GAME_MODES {
                // Break
                irqcall(0);
            }

            x = a = a << 1;
            gamemode_function_table[unaligned x]();
        }
    }
}

}

