// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "memmap";

import "room";
import "gamemodes";
import "gamestate";
import "metatiles";
import "metasprites";
import "resources";
import "room-transitions";
import "ui";
import "wait-frame";

import "player";
import "entityloop";
import "entities/_variables";
import "entities/player-bomb";
import "room-events/_callers";

// ::TODO find a better place to call `rou2s_commands.process_commands()`::
import "rou2s-commands";


namespace gameloop {


in code {


// Process the game loop
//
// ASSUMES: PPU setup and screen enabled
//
// DB = 0x7e
#[mem8, idx8]
func run_game {

    ^do {
        entities.player_bomb.prepare_explosion_variables__inline();

        room_events.process__inline();

        // Always process player entity first
        entities.currentEntityId = PLAYER_ENTITY_ID;
        player.process();
        metasprites.process_entity_animation(PLAYER_ENTITY_ID);

        idx16();
        #[mem8, idx16] {
            player.update_collision_boxes__inline();
        }
        mem8_idx8();


        entityloop.process_active_entities();

        entityloop.sort_active_entities();

        draw_entities();


        ui.process();


        _check_for_room_transition();


        a = ui.healthBarValue;
        if zero {
            a = GameModes.GAME_OVER as u8;
            gameMode = a as GameModes;

            break;
        }

        // ::TODO find a better spot to put this::
        // Cannot put it in `wait_frame()`, it might use `zpTmp` variables
        rou2s_commands.process_commands();

        wait_frame();


        // loop until gameMode changes

        // ASSERT: GameModes.RUN_GAME == 0
        a = gameMode as u8;
    } while zero;
}



// Process the game loop
//
// DB = 0x7e
#[mem8, idx8]
func draw_entities() {
    idx16();
    #[idx16] {
        metasprites.reset_buffer_and_draw_entity_shadows();
    }
    idx8();

    entityloop.draw_active_entities();

    idx16();
    #[idx16] {
        metasprites.finalize_buffer();
    }
    idx8();
}



// Check for a room transition and preform them as required.
//
// DB = 0x7e
#[mem8, idx8]
inline func _check_for_room_transition() {

    a = player.yPos.px;
    if zero {
        // Check if there is a room above this one
        a = gamestate.data.currentRoom - 0x10;
        if carry {
            a = GameModes.ROOM_TRANSITION_SCROLL_UP as u8;
            gameMode = a as GameModes;
        }

        // Do not check x axis after loading next room
        return;
    }
    else if a >= DISPLAY_HEIGHT - 1 {
        // Check if there is a room below this one
        a = gamestate.data.currentRoom + 0x10;
        if !carry {
            a = GameModes.ROOM_TRANSITION_SCROLL_DOWN as u8;
            gameMode = a as GameModes;
        }

        // Do not check x axis after loading next room
        return;
    }

    a = player.xPos.px;
    if zero {
        // Check if there is a room to the left of this one
        a = gamestate.data.currentRoom & 0x0f;
        if !zero {
            a = GameModes.ROOM_TRANSITION_SCROLL_LEFT as u8;
            gameMode = a as GameModes;
        }
    }
    else if a >= DISPLAY_WIDTH - 1 {
        // Check if there is a room to the right of this one
        a = gamestate.data.currentRoom & 0x0f;
        if a != 0x0f {
            a = GameModes.ROOM_TRANSITION_SCROLL_RIGHT as u8;
            gameMode = a as GameModes;
        }
    }
}

}

}

