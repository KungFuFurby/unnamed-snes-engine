// Copyright (c) 2021, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "memmap";
import "../wiz/common/snes/snes";

import "metatiles";
import "metasprites";
import "resources";
import "entities/_variables";
import "entities/_common";
import "entities/player";


namespace gameloop {


let DISPLAY_WIDTH = 256;
let DISPLAY_HEIGHT = 224;


let ENTITIES_IN_MAP = 8;

// Map data format
struct RoomData {
    map         : [u8 ; metatiles.MAP_WIDTH * metatiles.MAP_HEIGHT],

    tileset     : u8,

    // Entities in the map
    // (Structure of Arrays)
    // If the entity does not spawn then the array elements MUST be 0xff;
    entity_xPos : [u8 ; ENTITIES_IN_MAP ],
    entity_yPos : [u8 ; ENTITIES_IN_MAP ],
    entity_type : [u8 ; ENTITIES_IN_MAP ],
};



in zeropage {

}

in wram7e {

    // This hack is required to access room via addr addressing.
    #[align(256)]
    var __far_room : RoomData;
    let room = *(&__far_room as *RoomData);


    // The location of the current room
    var currentRoom : u8;

}


in code {

// Initialize the game loop
//
// REQUIRES: Force Blank
//
// DB = 0x7e
#[mem8, idx8]
inline func init__forceblank() {
    // ::TODO setup screen (if necessary)::

    entities.player.init();

    currentRoom = a = resources.rooms.STARTING_ROOM;

    _load_room();
    _load_resources__forceblank();

    metatiles.populate_map_tile_properties();
    metatiles.draw_map__forceblank();

    _spawn_room_entities();
}



// Load a room into memory
//
// DB = 0x7e
#[mem8, idx8]
func _load_room() {
let _tmp = <:dpTmp0;
let _roomPtr = dpTmp_farPtr;

    x = currentRoom;
    a = resources.rooms.room_table[x];

    a--;
    if a >= resources.rooms.N_ROOMS {
        a = 0;
    }
    x = a;

    <:_roomPtr = a = resources.rooms.room_list_l[x];
    >:_roomPtr = a = resources.rooms.room_list_h[x];
    #:_roomPtr = a = resources.rooms.room_list_bank;


    y = sizeof(typeof(room)) - 1;
    do {
        (&room as *u8)[y] = a = _roomPtr[y];
        y--;
    } while !zero;

    (&room as *u8)[0] = a = *_roomPtr;
}



// Load resources into memory and PPU.
//
// DB = 0x7e
#[mem8, idx8]
func _load_resources__forceblank() {
    idx16();
    #[idx16] {
        push8(data_bank);
        push8(program_bank);
        data_bank = pop8();
    // DB = 0x80

        metatiles.load_tileset__forceblank(room.tileset);

        // ::TODO get spritesheet_id from somewhere::
        metasprites.load_ppu_data__fblank(0);


        data_bank = pop8();
    // DB = 0x7e
    }
    idx8();
}


// Spawn entity in the room
//
// DB = 0x7e
#[mem8, idx8]
func _spawn_room_entities() {
    entities.init();


    x = ENTITIES_IN_MAP - 1;
    do {
        push8(x);

        entities.spawn_entity(a = room.entity_xPos[x],
                              a = room.entity_yPos[x],
                              a = room.entity_type[x]);

        x = pop8();

        x--;
    } while !negative;

}



// Change the brightness of the display.
//
// NOTE: Will also wait until the end of the current frame
//
// DB = 0x7e
#[mem8, idx8]
func wait_frame_and_change_brightness(brightness : u8 in a) {

    // ::TODO WaitFrame::
    wait_until_interrupt();

    *(far &snes.ppu.brightness) = a;
}



// Process the game loop
//
// DB = 0x7e
#[mem8, idx8]
func process() {

    // Always process player entity first
    x = 0;
    entities.SoA.process_function[unaligned x](x);


    entities.process_active_entities();



    // Check for a room transition
    a = entities.player.yPos.px;
    if zero {
        _change_room_north();
    }
    else if a >= DISPLAY_HEIGHT - 1 {
        _change_room_south();
    }

    a = entities.player.xPos.px;
    if zero {
        _change_room_west();
    }
    else if a >= DISPLAY_WIDTH - 1 {
        _change_room_east();
    }
}



// Process the game loop
//
// DB = 0x7e
#[mem8, idx8]
func draw_entities() {
    entities.draw_active_entities();
}



// The player has reached the top of the screen and wants to enter the room above the `currentRoom`.
//
// DB = 0x7e
#[mem8, idx8]
inline func _change_room_north() {

    a = currentRoom - 0x10;
    if carry {
        currentRoom = a;

        entities.player.yPos.px = a = DISPLAY_HEIGHT - 2;
        entities.player.yPos.sx = 0;

        _room_transition__blank();
    }
}



// The player has reached the bottom of the screen and wants to enter the room below the `currentRoom`.
//
// DB = 0x7e
#[mem8, idx8]
inline func _change_room_south() {

    a = currentRoom + 0x10;
    if !carry {
        currentRoom = a;

        entities.player.yPos.px = a = 1;
        entities.player.yPos.sx = 0;

        _room_transition__blank();
    }
}



// The player has reached the right edge of the screen and wants to enter the room right of `currentRoom`.
//
// DB = 0x7e
#[mem8, idx8]
inline func _change_room_east() {

    a = (currentRoom & 0x0f) + 1;
    if !carry {
        // Can move eastward
        currentRoom++;

        entities.player.xPos.px = a = 1;
        entities.player.xPos.sx = 0;

        _room_transition__blank();
    }
}



// The player has reached the left edge of the screen and wants to enter the room left of `currentRoom`.
//
// DB = 0x7e
#[mem8, idx8]
inline func _change_room_west() {

    a = (currentRoom & 0x0f) - 1;
    if carry {
        // Can move westward
        currentRoom--;

        entities.player.xPos.px = a = DISPLAY_WIDTH - 2;
        entities.player.xPos.sx = 0;

        _room_transition__blank();
    }
}



// Blank the screen, then load the next room and resources.
//
// DB = 0x7e
#[mem8, idx8]
func _room_transition__blank() {

    wait_frame_and_change_brightness(0x08);

    _load_room();
    metatiles.populate_map_tile_properties();
    _spawn_room_entities();


    wait_frame_and_change_brightness(snes.ppu.BRIGHTNESS_FORCE_BLANK | snes.ppu.BRIGHTNESS_MASK);

    _load_resources__forceblank();
    metatiles.draw_map__forceblank();
}




}

}

