// Gamestate variables
//
// Copyright (c) 2022, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.


import "memmap";
import "ui";


in lowram {
    // Number of frames processed.
    // (incremented every `WaitFrame` call)
    //
    // MUST be in the `lowram` bank to work correctly.
    //
    // (u32)
    var frameCounter : u32;

        var frameCounter_lobyte @ &<:frameCounter : u8;

        var frameCounter_loword @ &frameCounter              : u16;
        var frameCounter_hiword @ (&frameCounter as u16 + 2) : u16;
}


namespace gamestate {

in wram7e_roomstate {
    // Game State flags
    // (256 bit bitfield)
    var gameFlags : [ u8 ; 256 / 8 ];


    // Number of keys held by the player
    //
    // NOTE: This function should not be modified by game code
    var _keyCount : u8;
}


in code {

// Initialize the gamestate
//
// DB = 0x7e
#[mem8, idx8]
inline func init() {
    // Clear all the game flags
    x = sizeof(typeof(gameFlags)) - 1;
    do {
        gameFlags[x] = 0;
        x--;
    } while !negative;

    _keyCount = 0;
}



// Returns true if the game flag is clear
//
// DB = 0x7e
#[mem8, idx8]
func is_flag_clear(flag_id : u8 in x) : bool in zero {
    y = a = x >>> 3;
    x = a = x & 7;

    a = gameFlags[y] & _BitTable[x];
    return zero;
}


// Returns true if the game flag is clear (without clobbering the Y register)
//
// KEEP: y
//
// DB = 0x7e
#[mem8, idx8]
func is_flag_clear__keep_y(flag_id : u8 in x) : bool in zero {
var _tmp : u8 in zpTmp_byte_0;

    _tmp = y;

    y = a = x >>> 3;
    x = a = x & 7;

    a = gameFlags[y];

    y = _tmp;

    a &= _BitTable[x];
    return zero;
}


// DB = 0x7e
#[mem8, idx8]
func set_game_flag(flag_id : u8 in x) {
    y = a = x >>> 3;
    x = a = x & 7;

    gameFlags[y] = a = gameFlags[y] | _BitTable[x];
}



// Increment the key counter
// DB = 0x7e
#[mem8, idx8]
func increment_key_count() {
    x = _keyCount;
    x++;
    if negative {
        x = 127;
    }
    _keyCount = x;

    return ui.draw_key_bar(x);
}



// Decrement the key counter.
//
// Return true if there was a key to decrement.
//
// DB = 0x7e
#[mem8, idx8]
func test_and_decrement_key_count() : bool in carry{
    x = _keyCount;
    if !zero {
        x--;
        _keyCount = x;

        ui.draw_key_bar(x);
        return true;
    }
    return false;
}


}


in rodata0 {

const __BitTable : [ u8 ; 8 ] = [ 1 << i for let i in 0..7 ];

let _BitTable = far &__BitTable as far *u8;

}

}

