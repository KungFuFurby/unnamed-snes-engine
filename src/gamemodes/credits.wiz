// Copyright (c) 2022, Marcus Rowe <undisbeliever@gmail.com>.
// Distributed under The MIT License, see the LICENSE file for more details.

import "../memmap";
import "../common/reset";

import "../../wiz/common/snes/snes";

import "../gamemodes";
import "../joypad";
import "../resources";
import "../wait-frame";


namespace gamemodes {
namespace credits {


// The credits screen uses a custom VRAM map

let VRAM_BG1_MAP_WADDR   = 0x0000;  // 32x32
let VRAM_BG2_MAP_WADDR   = 0x0800;  // 32x64

let VRAM_BG1_TILES_WADDR = 0x1000;
let VRAM_BG2_TILES_WADDR = 0x2000;


let N_MIT_LICENSES = 2;
let MIT_LICENSE_TEXT_MAX_YPOS = 202;


in code {



// Credits and licenses screen
//
// DB = 0x7e
#[mem8, idx8]
func exec() {
    wait_multiple_frames_and_fadeout__slow();

    push16(0x7e80);
    data_bank = pop8();
// DB = 0x80

    idx16();
    #[idx16] {
        setup_ppu__credits();
    }
    mem8_idx8();

    data_bank = pop8();
// DB = 0x7e

    wait_multiple_frames_and_fadein();

    process__credits();


    wait_multiple_frames_and_fadeout();

    push16(0x7e80);
    data_bank = pop8();
// DB = 0x80

    idx16();
    #[idx16] {
        setup_ppu__licenses();
    }
    mem8_idx8();

    data_bank = pop8();
// DB = 0x7e

    wait_multiple_frames_and_fadein();

    process__licenses();


    a = GameModes.TITLE_SCREEN as u8;
    gameMode = a as GameModes;
}



// Setup the PPU for the credits image
//
// DB = 0x80
#[mem8, idx16]
inline func setup_ppu__credits() {

    reset_registers();

    // ::TODO move elsewhere::
    mem16();
    #[mem16] {
        bg1_scrollX = 0;
        bg1_scrollY = 0;
    }
    mem8();

    snes.ppu.bg_mode = a = 0;
    snes.ppu.bg1_tilemap = a = (VRAM_BG1_MAP_WADDR >> snes.ppu.BG_TILEMAP_ADDRESS_BIT_ALIGN << snes.ppu.BG_TILEMAP_ADDRESS_SHIFT_LEFT) | snes.ppu.BG_TILEMAP_MIRROR_X;
    snes.ppu.bg12_tileset = a = (VRAM_BG1_TILES_WADDR >> snes.ppu.BG_TILESET_ADDRESS_BIT_ALIGN << snes.ppu.BG12_TILESET_ADDRESS_BG1_SHIFT);
    snes.ppu.screen_enable_mask = a = snes.ppu.SCREEN_ENABLE_BG1;

    resources.load_bg_image_resource_into_vram(resources.bg_images.credits, VRAM_BG1_MAP_WADDR, VRAM_BG1_TILES_WADDR);
}


// Process the credits image
// DB = 0x7e
#[mem8, idx8]
inline func process__credits() {

    // Wait until the player pressed start
    // Or 8 seconds have elapsed
    mem16();
    #[mem16] {
        do {
            wait_frame();

            aa = joypad.pressed;
        } while aa & joypad.JOY.START == 0;
    }
    mem8_idx8();
}




// Setup the PPU for the licences screen
//
// DMA: Uses DMA channel 7
//
// DB = 0x80
#[mem8, idx16]
inline func setup_ppu__licenses() {

    reset_registers();

    // ::TODO move elsewhere::
    mem16();
    #[mem16] {
        bg1_scrollX = 0;
        bg1_scrollY = 0;
        bg2_scrollX = 0;
        bg2_scrollY = 0;
    }
    mem8();

    snes.ppu.bg_mode = 0;
    snes.ppu.bg12_tileset = a = (VRAM_BG1_TILES_WADDR >> snes.ppu.BG_TILESET_ADDRESS_BIT_ALIGN << snes.ppu.BG12_TILESET_ADDRESS_BG1_SHIFT)
                                | (VRAM_BG2_TILES_WADDR >> snes.ppu.BG_TILESET_ADDRESS_BIT_ALIGN << snes.ppu.BG12_TILESET_ADDRESS_BG2_SHIFT);
    snes.ppu.bg1_tilemap = a = (VRAM_BG1_MAP_WADDR >> snes.ppu.BG_TILEMAP_ADDRESS_BIT_ALIGN << snes.ppu.BG_TILEMAP_ADDRESS_SHIFT_LEFT);
    snes.ppu.bg2_tilemap = a = (VRAM_BG2_MAP_WADDR >> snes.ppu.BG_TILEMAP_ADDRESS_BIT_ALIGN << snes.ppu.BG_TILEMAP_ADDRESS_SHIFT_LEFT) | snes.ppu.BG_TILEMAP_MIRROR_Y;
    snes.ppu.screen_enable_mask = a = snes.ppu.SCREEN_ENABLE_BG1 | snes.ppu.SCREEN_ENABLE_BG2;

    resources.load_bg_image_resource_into_vram(resources.bg_images.licenses_bg1, VRAM_BG1_MAP_WADDR, VRAM_BG1_TILES_WADDR);
    resources.load_bg_image_resource_into_vram(resources.bg_images.licenses_bg2, VRAM_BG2_MAP_WADDR, VRAM_BG2_TILES_WADDR);

    // Set BG2 palette
    snes.ppu.cgram_address = a = 32 + 1;
    snes.ppu.cgram_write_data = a = 0xff;
    snes.ppu.cgram_write_data = a = 0xff;


    // Setup a HDMA to hide the second half of BG1
    snes.dma7.control = a = snes.dma.CONTROL_MODE_A;
    snes.dma7.mdma_dest = a = <:&snes.ppu.screen_enable_mask;
    snes.dma7.src_address_hl = xx = &HdmaTable as u16;
    snes.dma7.src_address_bank = a = #:far &HdmaTable;

    snes.dma.hdma_enable = a = snes.dma.ENABLE_7;
}


in rodata0 {

const HdmaTable : [ u8 ] = [
    88, snes.ppu.SCREEN_ENABLE_BG1,
    1, snes.ppu.SCREEN_ENABLE_BG2,
    0,
];


}



// Process the open source licences
// DB = 0x7e
#[mem8, idx8]
inline func process__licenses() {

    idx16();
    #[idx16] {
        xx = 0;
        yy = 0;

        do {
            bg1_scrollY = xx;
            bg2_scrollY = yy;

            wait_frame();

            xx = bg1_scrollY;
            yy = bg2_scrollY;

            a = joypad.current_h;
            if a & joypad.JOYH.LEFT != 0 {
                // Show first license
                xx = 0;
                yy = 0;
            }
            else if a & joypad.JOYH.RIGHT != 0 {
                // Show second license
                xx = 128;
                yy = 0;
            }
            else {
                if a & joypad.JOYH.UP != 0 {
                    yy--;
                    if negative {
                        yy = 0;
                    }
                }
                else if a & joypad.JOYH.DOWN != 0 {
                    yy++;
                    if yy >= MIT_LICENSE_TEXT_MAX_YPOS {
                        yy = MIT_LICENSE_TEXT_MAX_YPOS;
                    }
                }
            }

            a = joypad.pressed_h;
            if a & joypad.JOYH.START != 0 {
                // Show the next license
                mem16();
                #[mem16] {
                    xx = aa = bg1_scrollY + 128;
                    yy = 0;
                }
                mem8();
            }

            // Must break the loop before writing to bg/bg scroll shadows to prevent a screen glitch on fadeout.
        } while xx < N_MIT_LICENSES * 128;
    }
    mem8_idx8();
}

}

}
}

